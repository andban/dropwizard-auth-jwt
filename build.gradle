apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = 'de.borntohula.dropwizard'
archivesBaseName = 'dropwizard-auth-jwt'
version = '0.1.2-SNAPSHOT'

project.ext {
    dropwizardVersion = '0.8.1'
    jose4jVersion = '0.4.1'
    jerseyVersion = '2.17'

    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'io.dropwizard', name: 'dropwizard-auth', version: project.dropwizardVersion
    compile group: 'org.bitbucket.b_c', name: 'jose4j', version: project.jose4jVersion

    testCompile group: 'io.dropwizard', name: 'dropwizard-testing', version: project.dropwizardVersion
    testCompile group: 'org.glassfish.jersey.test-framework', name: 'jersey-test-framework-core', version: project.jerseyVersion
    testCompile group: 'org.glassfish.jersey.test-framework.providers', name: 'jersey-test-framework-provider-grizzly2', version: project.jerseyVersion
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask(':uploadArchives') && isReleaseVersion }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Dropwizard JWT Authentication'
                packaging 'jar'

                description 'JSON Web Token based authentication for Dropwizard.'
                url 'https://github.com/andban/dropwizard-auth-jwt'

                scm {
                    connection 'scm:git:git@github.com:andban/dropwizard-auth-jwt.git'
                    developerConnection 'scm:git:git@github.com:andban/dropwizard-auth-jwt.git'
                    url 'https://github.com/andban/dropwizard-auth-jwt'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'andban'
                        name 'Andreas Bannach'
                        email 'andreas@borntohula.de'
                    }
                }
            }
        }
    }
}
